apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'jacoco'
apply plugin: 'application'
apply plugin: 'findbugs'

tasks.withType(FindBugs) {
    effort = "default"
    reportLevel = "high"

    reports {
        xml.enabled = false
        html.enabled = true
    }
 }

mainClassName = "bixie.Bixie"

task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}

dependencies {
    compile fileTree(dir: 'lib', include: '*.jar')
    testCompile "junit:junit:4.11"  // Or whatever version
}

repositories {
    jcenter()
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.destination "${buildDir}/reports/coverage"
    }
}


sourceCompatibility = 1.7
version = '1.0'
jar {
	baseName = 'bixie'

    manifest {
        attributes 'Main-Class': 'bixie.Bixie',
                   'Class-Path': '.',
                   'Implementation-Title': 'Bixie',
                   'Implementation-Version': version
    }

    from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
}

test {
	jacoco {
		enabled = true
	}

    testLogging {
        events "failed"
        exceptionFormat "full"
    }

   	useJUnit()
}

task selfCheck {
  group 'Verification'
  description 'Run Bixie on itself.'

  doLast {
    def bixieJar = jar.archivePath
    def bixieDir = compileJava.destinationDir
    def bixieClassPath = compileJava.classpath.asPath
    //TODO generate this.
    def bixieReportDir = './build/reports/bixie/out.txt'

    exec {
        workingDir '.'
        commandLine 'java', '-jar', bixieJar, '-j', bixieDir, '-cp', bixieClassPath, '-o', bixieReportDir
    }

  }
}